#!/usr/bin/perl -w

use strict;
use File::Temp qw/tempfile/;
use Getopt::Long;

our $state = 'null';
our $data  = '';
our @stream = ();
our %frag = ();

our %opts = ();
our $V = 0;

sub debug1 { if ($V >= 1) { print STDERR @_; } }
sub debug2 { if ($V >= 2) { print STDERR @_; } }

sub get_options()
{
	my $theme   = '';
	my $header  = '';
	my $footer  = '';
	my $css     = '';
	my $help    = 0;
	my $strip   = './';
	my $root    = '.';
	Getopt::Long::Configure('bundling');
	GetOptions(
		'strip=s'  => \$strip,
		's=s'      => \$strip,

		'root=s'   => \$root,
		'R=s'      => \$root,

		'header=s' => \$header,
		'H=s'      => \$header,

		'footer=s' => \$footer,
		'F=s'      => \$footer,

		'css=s'    => \$css,
		'C=s'      => \$css,

		'theme=s'  => \$theme,
		'T=s'      => \$theme,

		'verbose+' => \$V,
		'v+'       => \$V,

		'help'     => \$help,
		'h'        => \$help
	);

	if ($theme ne '') {
		if ($header eq '') { $header = "$theme/header.html"; }
		if ($footer eq '') { $footer = "$theme/footer.html"; }
		if ($css    eq '') { $css    = "$theme/style.css";   }
	}

	$opts{header} = $header;
	$opts{footer} = $footer;
	$opts{css}    = $css;
	$opts{strip}  = $strip;
	$opts{root}   = $root;

	debug1 "Setting verbosity to $V\n";

	if ($help) {
		print "$0 - C Documentor\n";
		print "USAGE: $0 [options] file [ file ... ]\n";
		print "\n";
		print "Options:\n";
		print " --strip path   Path component to remove from source file names.\n";
		print " -s path\n";
		print "\n";
		print " --root path    Place all output in path.  Defaults to current\n";
		print " -R path        working directory (.).\n";
		print "\n";
		print " --theme dir    Override the default header, footer and CSS content\n";
		print " -T dir         by using the `header.html', `footer.html' and `style.css'\n";
		print "                files found in dir; a rudimentary form of theming.\n";
		print "\n";
		print "                Note: the -H, -F and -C options override -T.\n";
		print "\n";
		print " --header file  Specify an alternate HTML fragment to use for\n";
		print " -H file        the beginning chunk of the output document.\n";
		print "                If not specified, a suitable default (bundled with\n";
		print "                cdoc) will be used.\n";
		print "\n";
		print " --footer file  Specify an alternate HTML fragment to use for\n";
		print " -F file        the ending chunk of the output document.\n";
		print "                If not specified, a suitable default (bundled with\n";
		print "                cdoc) will be used.\n";
		print "\n";
		print " -css file      Specify an alternate Cascading Stylesheet for the\n";
		print " -C file        output document.  If not specified, a suitable default\n";
		print "                (bundled with cdoc) will be used.\n";
		print "\n";
		print " --help, -h     Show this help screen.\n";
		print "\n";
		print " --verbose      Generate more output; can be used more than once.\n";
		print " -v\n";
		print "\n";
		exit(0);
	}
}

sub fragments()
{
	my %h = ();
	my $key = ''; my $val = '';
	while (<DATA>) {
		if (m/^-[a-z]+-{6,}/) {
			if ($val ne '') { $h{$key} = $val; }

			chomp;
			s/^-([a-z]+)-{6,}/$1/;
			$key = $_;
			$val = '';
			next;
		}
		$val .= $_;
	}
	if ($val ne '') { $h{$key} = $val; }
	close DATA;

	# Override from %opts
	if ($opts{header} ne '') { $h{header} = getfile($opts{header}); }
	if ($opts{footer} ne '') { $h{footer} = getfile($opts{footer}); }
	if ($opts{css}    ne '') { $h{css}    = getfile($opts{css});    }

	return %h;
}

sub bail($)
{
	my $error = shift;
	print STDERR "$error\n";
	exit(1);
}

sub putfile($$)
{
	my $file = shift;
	my $data = shift;

	my $fh;
	open  $fh, ">$file";
	print $fh $data;
	close $fh;
}

sub getfile($$)
{
	my $file = shift;
	my $data = '';

	my $fh;
	open  $fh, "<$file" or bail("$file: $!");
	while (<$fh>) { $data .= $_; }
	close $fh;

	return $data;
}

sub is_typedef_struct($)
{
	local $_ = shift;
	return m/^typedef\s+struct/;
}

sub is_struct($)
{
	local $_ = shift;
	return m/^struct\s+\S+\s*{/;
}

sub is_typedef_enum($)
{
	local $_ = shift;
	return m/^typedef\s+enum/;
}

sub is_enum($)
{
	local $_ = shift;
	return m/^enum\s+\S+\s*{/;
}

sub is_macro($)
{
	local $_ = shift;
	return m/^#define \w+\s*\(/;
}

sub filename($)
{
	local $_ = shift;
	s/^$opts{strip}//;
	return $_;
}

sub proto($)
{
	local $_ = shift;
	if (is_typedef_struct $_) {
		s/\s*{.*}\s+(\w+);.*/ { ... } $1/s;
		return $_;
	}
	if (is_macro $_) {
		s/^#define\s*([^\)]+\)).*/$1/s;
		return $_;
	}
	s/\s+{.*//s;
	return $_;
}

sub objkey($)
{
	local $_ = shift;
	if (is_typedef_struct($_) or is_typedef_enum($_)) {
		s/typedef\s+(struct|enum)\s+{.*}\s+(\w+);.*/$2/s;
		return $_;
	}
	if (is_struct($_) or is_enum($_)) {
		s/\s+{.*//s;
		return $_;
	}
	if (is_macro $_) {
		s/^#define\s*([^\(]+)\(.*/$1/s;
		return $_;
	}
	s/\s+{.*//s;
	s/\s*\(.*//;
	s/.*\s+\**//;
	return $_;
}

sub key2id($)
{
	local $_ = shift;
	s/\s+/-/g;
	return $_;
}

sub htmlify($)
{
	local $_ = shift; s/&/\&amp;/g; s/</\&lt;/g; s/>/\&gt;/g; return $_;
}

sub esc($)
{
	local $_ = shift;
	my @l = split /\n/;
	my $s = '';
	for (@l) {
		if (m|</code>|) { $s = ''; }
		if ($s eq 'code') { $_ = htmlify($_); next; }
		if (m|<code>|)  { $s = 'code'; }
	}
	return join("\n", @l);
}

sub markdown
{
	my ($code, $config) = @_;

	# drop the comments
	#$code =~ s|^//.*\n||gm;
	#$code =~ s|(\S)\s+//.*|$1|g;

	my ($fh, $file) = tempfile;
	binmode($fh, ":utf8");
	print $fh $code; close $fh;
	$code = qx(/usr/bin/markdown --html4tags <$file);
	unlink $file;

	$code =~ s/^\s+//;
	$code =~ s/\s+$//;
	return $code;
}

sub pretty($)
{
	local $_ = esc(shift);
	s/<code>(\s*\n)*/<pre><code>/g;
	s#\s*\n\s*</code>#</code></pre>#g;
	local $_ = markdown($_);
	s#([^\\]|^)\$([a-zA-Z0-9_]+)#$1<var>$2</var>#g;
	s/([^\\])@([a-zA-Z0-9_]+)/$1<a href="#$2">$2<\/a>/g;
	s/\\(\$|@)/$1/g;
	return $_;
}

sub summary($)
{
	local $_ = shift;
	s/\n\n+.*//s;
	return $_;
}

sub detail($)
{
	local $_ = shift;
	s/[^\n]*\n\n+//;
	return $_;
}

sub mapstream
{
	my %map = ();
	my $h; my $k;
	my $comment = '';
	my $file = '';
	my $line = 0;

	for (@_) {
		my ($op, $s) = @{$_};
		if ($op eq 'comment') {
			$comment = $s;
		} elsif ($op eq 'file') {
			$file = filename($s);
		} elsif ($op eq 'line') {
			$line = $s;
		} elsif ($comment ne '') {
			$h = {};
			$k = objkey($s);
			if (is_typedef_struct($s) or is_struct($s)) {
				$h->{type} = 'struct';
				$k = "005:$k";
			} elsif (is_typedef_enum($s) or is_enum($s)) {
				$h->{type} = 'enum';
				$k = "001:$k";
			} else {
				$h->{type} = 'function';
				$k = "050:$k";
			}
			$h->{file}      = $file;
			$h->{line}      = $line;
			$h->{summary}   = summary($comment);
			$h->{comment}   = detail($comment);
			if ($h->{comment} eq $h->{summary}) {
				$h->{comment} = '';
			}
			$h->{prototype} = proto($s);
			$h->{code}      = $s;
			$map{$k} = $h;

			$comment = '';
		}
	}

	return %map;
}

sub parsefile($)
{
	my $file = shift;

	push @stream, ['file', $file];
	my $io;
	open  $io, "<$file";
	parse($io);
	close $io;
}

sub parse($)
{
	my $io = shift;
	my $n = 0; my $line = 0;
	my $first = 0;
	my $type = '';
	my $proto = '';

	while (<$io>) {
		$n++;
		s/\r//g;
		if (m/^\/\*\*(\s|$)/) { # are we at the start of a comment?
			s/^\/\*\*\s+//;
			if (m/\*\/$/) { # is it a single-line comment?
				s/\s*\*\/$//;
				push @stream, ['comment', $_];
				$state = 'code'; $data = ''; $line = $n;
			} else {
				$state = 'comment';
				$data = '';
			}
			next;
		}

		if ($state eq 'comment') {
			if (m/^\s*\*\/$/) {
				push @stream, ['comment', $data];
				$state = 'code'; $data = ''; $line = $n;
				next;
			}
			s/\s{2}//;
			$data .= $_;
			next;
		}

		if ($state eq 'code') {
			$first = 0;
			if ($data eq '') {
				$type = m/^#define/ ? 'macro' : 'other';
				$first = 1;
			}
			$data .= $_;
			if (($type ne 'macro' and /^}(\s+\w+)?;?$/)
			 || ($type eq 'macro' and /^\s*$/)
			 || ($type ne 'macro' and $first and /\s+}$/)) {
				push @stream, ['line', $line+1];
				push @stream, ['code', $data];

				$proto = objkey($data);
				debug2 "  - $proto\n";

				$state = 'null'; $data = '';
				next;
			}
		}
	}

	$state = 'null'; $data = '';
}

sub output()
{
	my $out;
	open $out, ">$opts{root}/index.html" or die "Couldn't open output file: $!";

	print $out $frag{header};
	my %map = mapstream(@stream);
	for my $name (sort keys %map) {
		my %o = %{$map{$name}};
		$name =~ s/.\d+://;

		$o{comment} = pretty($o{comment});
		$o{summary} = pretty($o{summary});
		$o{summary} =~ s/<p>/<p class="summary">/;

		print $out "    <div class=\"doc $o{type}\" id=\"", key2id($name), "\">\n";
		print $out "      <h2>$name</h2>\n\n";
		if ($o{type} ne 'struct' and $o{type} ne 'enum') {
			print $out "      <p class=\"prototype\">$o{prototype}</p>\n\n";
		}
		print $out $o{summary};
		if ($o{type} eq 'struct' or $o{type} eq 'enum') {
			print $out "      <pre class=\"$o{type}\">$o{code}</pre>\n";
		}
		print $out $o{comment};
		print $out "\n";
		if ($o{type} ne 'struct') {
			print $out "      <a href=\"#\" class=\"source\"><code>$o{file}:$o{line}</code></a>\n";
			my $code = htmlify($o{code});
			print $out "      <pre class=\"source\" id=\"src_$name\"><code>$code</code></pre>\n\n";
		} else {
			print $out "      <code>$o{file}:$o{line}</code>\n";
		}
		print $out "    </div>\n\n";
	}
	print $out $frag{footer};

	close $out;
}

get_options();
if (@ARGV == 0) { bail "No source files given."; }

%frag = fragments();
my $n = 0;
for my $f (@ARGV) {
	if (! -f $f) { next; }
	$n++;
	debug1 "> $f...\n";
	parsefile($f);
}

if ($n == 0) { bail "No source files given."; }

output();
putfile "$opts{root}/cdoc.css", $frag{css};
putfile "$opts{root}/cdoc.js",  $frag{js};

=head1 NAME

cdoc - Embed Documentation in C Source Code

=head1 SYNOPSIS

cdoc *.h *.c

cdoc --root /docs --theme ~/mytheme *.h *.c

cdoc --strip src src/*.c src/*.h

=head1 DESCRIPTION

cdoc is a small utility that will look at C source and header
files, extract documentation about structures and functions,
and render nice, clean HTML documentation to disk.

=head1 OPTIONS

=over

=item B<-s>, B<--strip> PATH

Path component to remove from source file names.

=item B<-R>, B<--root> PATH

Place all output in path.  Defaults to current working directory (.).

=item B<-T>, B<--theme> PATH

Override the default header, footer and CSS content by using the
C<header.html>, C<footer.html> and C<style.css> files found in the given
path; a rudimentary form of theming.

Note: the B<--header>, B<--footer> and B<--css> options override
B<--template>.

=item B<-H>, B<--header> FILE

Specify an alternate HTML fragment to use for the beginning chunk of the
output document.  If not specified, a suitable default (bundled with cdoc)
will be used.

=item B<-F>, B<--footer> FILE

Specify an alternate HTML fragment to use for the ending chunk of the output
document.  If not specified, a suitable default (bundled with cdoc) will be
used.

=item B<-C>, B<--css> FILE

Specify an alternate Cascading Stylesheet for the output document.  If not
specified, a suitable default (bundled with cdoc) will be used.

=item B<-v>, B<--verbose>

Generate more output; can be used more than once.

=back

=head1 AUTHOR

Written by James Hunt B<< <james@jameshunt.us> >>.

=cut
